apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'application'

mainClassName = 'org.numenta.nupic.examples.napi.hotgym.NetworkAPIDemo'

group = 'org.numenta'
version = '0.6.12'
archivesBaseName = 'htm.java-examples'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest {
        attributes 'Implementation-Title': 'htm.java-examples', 'Implementation-Version': '0.6.12'
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'maven central' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()

    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "./libs" }
    
}

dependencies {
    compile group: 'org.numenta', name: 'htm.java', version: '0.6.12'
    compile group: 'cortical', name: 'cortical-fxwidgets', version: '0.9.1'
    compile group: 'cortical', name: 'cortical-twitterutils', version: '0.9.1'
    compile group: 'joda-time', name: 'joda-time', version: '2.5'
    compile(group: 'com.chaschev', name: 'chutils', version:'1.4') {
        exclude(module: 'mockito-all')
        exclude(module: 'slf4j-api')
        exclude(module: 'guava')
        exclude(module: 'jsr305')
        exclude(module: 'commons-io')
    }
    compile group: 'net.sf.trove4j', name: 'trove4j', version:'3.0.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.4.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version:'2.4.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.4.4'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.10'
	compile group: 'io.reactivex', name: 'rxjava', version: '1.0.10'
	compile group: 'io.cortical', name: 'retina-api-java-sdk', version: '1.0.2'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.2'
}

/////////////////////////////////////////////////////////////////
//                   SonaType Central Repo                     //
/////////////////////////////////////////////////////////////////
task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
	required { gradle.taskGraph.hasTask("uploadArchives") }

    sign configurations.archives
}

uploadArchives {
  javadoc.failOnError = false

  if(!project.hasProperty('ossrhUsername')) {
	return
  }

  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Numenta htm.java-examples'
        packaging 'jar'
        artifactId 'htm.java'
        description "Example Apps for the Java version of Numenta's HTM technology."
        url 'https://github.com/numenta/htm.java-examples'

        scm {
          connection 'scm:git:git://github.com/numenta/htm.java-examples.git'
          developerConnection 'scm:git:git://github.com/numenta/htm.java-examples.git'
          url 'https://github.com/numenta/htm.java-examples'
        }

        licenses {
          license {
            name 'The GNU General Public License.'
            url 'https://github.com/numenta/htm.java-examples/blob/master/LICENSE.txt'
          }
        }
      }
    }
  }
}
/////////////////// END Central Repo ////////////////

// create Gradle wrapper with command line `gradle wrapper` in terminal
task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}
